import de.undercouch.gradle.tasks.download.Download

buildscript {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }

    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.2.0'
    }
}

intellij {
    version intellijIdeaVersion
    downloadSources intellijIdeaDownloadSources
}

task downloadPsiViewerPlugin(type: Download) {
    src psiViewerUrl
    dest new File(thirdPartyDir, 'PsiViewer.jar')
}

task preparePsiViewerPlugin(type: Copy) {
    from "$thirdPartyDir"
    into "$rootDir/build/idea-sandbox/plugins"
    include "PsiViewer.jar"

    dependsOn downloadPsiViewerPlugin
}

// Copy the "PsiViewer" plugin to the generated
// IntelliJ IDEA sandbox when developing locally.
// This allows inspecting the parse tree and each
// nodes properties.
tasks.prepareSandbox.finalizedBy(tasks.preparePsiViewerPlugin)